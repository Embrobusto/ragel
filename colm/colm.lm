lex
	token DEF / 'def' /
	token LEX / 'lex' /
	token END / 'end' /
	token TOKEN / 'token' /
	token IGNORE / 'ignore' /
	token PRINT / 'print' /
	token PARSE / 'parse' /

	token id / 
		( 'a' .. 'z' | 'A' .. 'Z' | '_' ) .
		( 'a' .. 'z' | 'A' .. 'Z' | '0' .. '9' | '_' ) *
	/

	token number / 
		( '0' .. '9' ) .
		( '0' .. '9' ) *
	/

	# FIXME: need the 'any' builtin.
	token lit /
		'\'' . ( ^( '\'' | '\\' ) | '\\' . ( 'a' .. 'z' | '\'' | '\\' ) )* .  '\''
	/

	token SQOPEN /'['/
	token SQCLOSE /']'/
	token BAR /'|'/
	token FSLASH /'/'/
	token COLON /':'/
	token DOT /'.'/
	token ARROW /'->'/
	token POPEN /'('/
	token PCLOSE /')'/
	token DOTDOT /'..'/
	token STAR /'*'/
	token CARET /'^'/

	ignore / 
		( '\n' | '\t' | ' ' ) .
		( '\n' | '\t' | ' ' )* 
	/

	ignore / '#' . ( ^'\n' )* . '\n' /
end


def start
	[RootItemList: root_item*]

def root_item
#	[literal_def]
#|	[rl_def]
#|	[token_def]
	[CflDef: cfl_def]
|	[RegionDef: region_def]
#|	[context_def]
#|	[namespace_def]
#|	[function_def]
#|	[iter_def]
#|	[global_def]
#|	[export_def]
|	[Statement: statement]
#|	[pre_eof]
#|	[precedence]
#|	[typedef]

def cfl_def
	[DEF DefId: id ProdList: prod_list]

def region_def
	[LEX TokenList: token_list END]

def token_list
	[TokenList: token_list TokenDef: token_def]
|	[TokenList: token_list IgnoreDef: ignore_def]
|	[]

def token_def
	[TOKEN Id: id FSLASH Expr: lex_expr FSLASH]

def ignore_def
	[IGNORE FSLASH Expr: lex_expr FSLASH]

def prod_el
	[OptName: opt_prod_name Id: id OptRepeat: opt_prod_repeat]

def opt_prod_name
	[Name: id COLON]
|	[]

def opt_prod_repeat
	[Star: STAR]
|	[]

def prod_el_list
	[ProdElList: prod_el_list ProdEl: prod_el]
|	[]

def prod
	[SQOPEN ProdElList: prod_el_list SQCLOSE]

def prod_list
	[ProdList: prod_list BAR Prod: prod]
|	[Prod: prod]

def statement
	[Print: print_stmt]
|	[Expr: expr_stmt]

def print_stmt
	[PRINT POPEN CodeExprList: code_expr* PCLOSE]

def expr_stmt
	[CodeExpr: code_expr]

def code_expr
	[Number: number]
|	[Lit: lit]
|	[VarRef: var_ref POPEN CodeExprList: code_expr* PCLOSE]
|	[VarRef: var_ref]
|	[PARSE opt_capture TypeRef: type_ref opt_field_init accumulate]

def type_ref
	[RegionQual: region_qual Id: id OptRepeat: opt_repeat]

def region_qual []
def opt_repeat []

def opt_capture
	[id COLON]
|	[]

def opt_field_init
	[]

def accumulate 
	[SQOPEN id SQCLOSE]

def var_ref
	[Qual: qual Id: id]

def qual
	[Qual: qual Id: id Dot: DOT]
|	[Qual: qual Id: id Arrow: ARROW]
|	[]

def lex_expr
	[Expr: lex_expr BAR Term: lex_term]
|	[Term: lex_term]

def lex_term
	[Term: lex_term DOT FactorRep: lex_factor_rep]
|	[FactorRep: lex_factor_rep]

def lex_factor_rep
	[FactorRep: lex_factor_rep STAR]
|	[FactorNeg: lex_factor_neg]

def lex_factor_neg
	[CARET FactorNeg: lex_factor_neg]
|	[Factor: lex_factor]

def lex_factor
	[Literal: lit]
|	[POPEN Expr: lex_expr PCLOSE]
|	[Low: lit DOTDOT High: lit]

