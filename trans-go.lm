include 'trans.lm'

namespace go
	lex 
		literal '%%{', '}%%'

		literal 'int', 'bool', 'const', 'char', 'ptr',
				'var', 'fmt.Print', 'byte'

		literal ';', '<', '>', '(', ')', '[', ']', '=',
				'*', '!', '{', '}', '+', '-', '==', '!=', ':'

		ignore / ( [ \t] | NL )+ /

		token tk_ident /ident/
		token tk_number /digit+/
		token tk_hex_number /'0x' [0-9a-fA-F]+/

		token string
			/ s_literal | d_literal /
	end

	def factor
		[tk_ident]
	|	[tk_ident '[' expr ']']
	|	[tk_ident '(' expr ')']
	|	[tk_number]
	|	[tk_hex_number]
	|	[string]
	|	['<' type '>' '(' expr ')']
	|	['(' type ')' '(' expr ')']
	|	['(' expr ')']
	|	[tk_ident ':' tk_ident]
	|	[':' tk_ident]

	def print_stmt
		['fmt.Print' '(' expr ')' ';']

	def op
		['<'] | ['>'] | ['='] | ['!']
	|	['+'] | ['-'] | ['*'] | ['!=']
	|	['==']

	def expr_op
		[op factor]

	def expr
		[factor expr_op*]

	def opt_arr
		['[' expr ']']
	|	[]

	def type
		['int']
	|	['bool']
	|	['char']
	|	['char' '*']
	|	['const' '(' 'char' ')' '*']
	|	['byte']

	def var_decl
		['var' tk_ident opt_arr type]
end

namespace indep

go::tk_ident go_ident( Ident: tk_ident )
{
	return cast<go::tk_ident> Ident
}

go::factor go_factor( Factor: factor )
{
	return cast<go::factor> Factor
}

go::expr go_expr( Expr: expr )
{
	return cast<go::expr> Expr
}

go::tk_ident go_ident( Ident: tk_ident )
{
	return cast<go::tk_ident> Ident
}

go::opt_arr go_opt_arr( OptArr: opt_arr )
{
	return cast<go::opt_arr> OptArr
}

go::type go_type( Type: type )
{
	return cast<go::type> Type
}

go::factor* go_tok_list( TokList: factor* )
{
	return cast<go::factor*> TokList
}

int go_rewrite( Indep: ref<any> )
{
	for Factor: factor in Indep {
		if match Factor "true" {
			Factor = cons factor "1"
		}
		elsif match Factor "false" {
			Factor = cons factor "0"
		}
		elsif match Factor ['<' Type: type '>' '(' Expr: expr ')'] {
			Factor = cast<factor> cons go::factor
				"( [go_type(Type)] ) ( [go_expr(Expr)] )"
		}
		elsif match Factor ['first_token_char'] {
			Factor = cast<factor> cons go::factor
				"data\[ts\]"
		}
	}

	for Decl: var_decl in Indep {
		if match Decl
			"bool [Ident: tk_ident OptArr: opt_arr];
		{
			Decl = cast<var_decl> cons go::var_decl
				"var [go_ident(Ident)] [go_opt_arr(OptArr)] int
		}
		elsif match Decl
			"int [Ident: tk_ident OptArr: opt_arr];
		{
			Decl = cast<var_decl> cons go::var_decl
				"var [go_ident(Ident)] [go_opt_arr(OptArr)] int
		}
		elsif match Decl
			"char [Ident: tk_ident OptArr: opt_arr];
		{
			Decl = cast<var_decl> cons go::var_decl
				"var [go_ident(Ident)] [go_opt_arr(OptArr)] byte
		}
		elsif match Decl
			"ptr [Ident: tk_ident];
		{
			Decl = cast<var_decl> cons go::var_decl
				"var [go_ident(Ident)] int
		}
	}

	for Stmt: print_stmt in Indep {
		if match Stmt ['printi' F: factor ';' ] {
			Stmt = cast<print_stmt> cons go::print_stmt
				"fmt.Print( [go_factor(F)] );"
		}
		elsif match Stmt ['printb' I: tk_ident ';' ] {
			Stmt = cast<print_stmt> cons go::print_stmt
				"fmt.Print( string ( [go_ident(I)]\[:pos\] ));"
		}
		elsif match Stmt ['prints' F: factor ';' ] {
			Stmt = cast<print_stmt> cons go::print_stmt
				"fmt.Print( [go_factor(F)] );"
		}
		elsif match Stmt ['print_token' ';' ] {
			Stmt = cast<print_stmt> cons go::print_stmt
				"fmt.Print( data\[ts:te\] );" 
		}
	}
}

end

OutputFile: str = argv.pop()
InputFile: str = argv.pop()
Lang: str = argv.pop()

Input: stream = open( InputFile "r" )
Output: stream = open( OutputFile "w" )

RagelTree: indep::start = parse indep::start[ Input ]

if ( !RagelTree ) {
	print( error '\n' )
	exit(1)
}

# Find the machine name.
MachineName: ragel::machine_name = ragel::machine_name in RagelTree

# Do we need EOF?
NeedsEof: bool = false
for CommVal: indep::comm_val in RagelTree {
	if $CommVal ==
		'@NEEDS_EOF: yes\n'
	{
		NeedsEof = true
	}
}

prints( Output
	"/*
	" * @LANG: go
	" * @GENERATED: yes
	" */
	"
	"package main
	"import \"fmt\"
	"
)

for Section: indep::section in RagelTree {
	if match Section [Decl: var_decl] {
		go_rewrite( Decl )
		send Output [Decl]
	}
}

for Section: indep::section in RagelTree {
	if match Section [section_multiline] {
		for Action: ragel::action_block in Section {
			# Reparse as lang-independent code.
			parse SectionList: indep::section*[$Action]

			# Translate to specific language.
			go_rewrite( SectionList )

			# Reparse back to ragel action block.
			Action = parse ragel::action_block[$SectionList]
		}

		prints( Output '\n' Section '\n' )
	}
}

send Output
	"var cs int
	"
	"%% write data;
	"
	"func prepare() {

for Section: indep::section in RagelTree {
	if match Section [ExprStmt: expr_stmt] {
		go_rewrite( ExprStmt )
		send Output [ExprStmt]
	}
}

send Output
	"	%% write init;
	"}
	"
	"func exec(data string) {
    "	var p int = 0
    "	var pe int = len(data)

if NeedsEof {
	send Output
		"	var eof int = pe
}

send Output
	"	%% write exec;
	"}
	"func finish() {
	"	if cs >= [MachineName.word]_first_final {
	"		fmt.Println(\"ACCEPT\")
	"	} else {
	"		fmt.Println(\"FAIL\")
	"	}
	"}

send Output
	~var inp []string = []string {

for InputString: indep::input_string in RagelTree {
	send Output [^InputString ",\n"]
}

send Output
	"};
	"

send Output
	"func main() {
	"	for _, data := range inp {
	"		prepare()
	"		exec(data)
	"		finish()
	"	}
	"}

send Output "/* _____OUTPUT_____
for OutputLine: indep::output_line in RagelTree {
	prints( Output OutputLine )
}
send Output "*/

