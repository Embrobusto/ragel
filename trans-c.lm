include 'trans.lm'

namespace c
	lex 
		literal '%%{', '}%%'

		literal 'int', 'bool', 'const', 'char', 'ptr',
				'printf', 'fwrite', 'var', 'byte'

		literal ';', '<', '>', '(', ')', '[', ']', '=',
				'*', '!', '{', '}', '+', '-', '==', '!=', ','

		ignore / ( [ \t] | NL )+ /

		token tk_ident /ident/
		token tk_number /digit+/
		token tk_hex_number /'0x' [0-9a-fA-F]+/

		token string
			/ s_literal | d_literal /
	end

	def factor
		[tk_ident]
	|	[tk_ident '[' expr ']']
	|	[tk_number]
	|	[tk_hex_number]
	|	[string]
	|	['(' type ')' '(' expr ')']
	|	['(' expr ')']

	def op
		['<'] | ['>'] | ['='] | ['!']
	|	['+'] | ['-'] | ['*'] | ['!=']
	|	['=='] | [',']

	def expr_op
		[op factor]

	def expr
		[factor expr_op*]

	def opt_arr
		['[' expr ']']
	|	[]

	def type
		['int']
	|	['bool']
	|	['char']
	|	['char' '*']
	|	['const' '(' 'char' ')' '*']
	|	['byte']

	def var_decl
		[type tk_ident opt_arr ';']

	def print_stmt
		['printf' '(' expr ')' ';']
	|	['fwrite' '(' expr ')' ';']
end

namespace indep

c::factor c_factor( Factor: factor )
{
	return cast<c::factor> Factor
}

c::tk_ident c_ident( Ident: tk_ident )
{
	return cast<c::tk_ident> Ident
}

c::opt_arr c_opt_arr( OptArr: opt_arr )
{
	return cast<c::opt_arr> OptArr
}

c::expr c_expr( Expr: expr )
{
	return cast<c::expr> Expr
}

int c_rewrite( Indep: ref<any> )
{
	for Factor: factor in Indep {
		if match Factor "true" {
			Factor = cons factor "1"
		}
		elsif match Factor "false" {
			Factor = cons factor "0"
		}
		elsif match Factor 
				['<' type '>' '(' Expr: expr ')']
		{
			Factor = cast<factor> cons c::factor 
				"( [c_expr(Expr)] )
		}
		elsif match Factor ['first_token_char'] {
			Factor = cast<factor> cons c::factor
				"ts\[0\]"
		}
	}

	for Decl: var_decl in Indep {
		if match Decl
			"bool [Ident: tk_ident OptArr: opt_arr];
		{
			Decl = cast<var_decl> cons c::var_decl
				"int [c_ident(Ident)] [c_opt_arr(OptArr)];
		}
		elsif match Decl
			"ptr [Ident: tk_ident];
		{
			Decl = cast<var_decl> cons c::var_decl
				"char* [c_ident(Ident)];
		}
	}

	for Stmt: print_stmt in Indep {
		if match Stmt ['printi' F: factor ';' ] {
			Stmt = cast<print_stmt> cons c::print_stmt
				"printf( \"%d\", [c_factor(F)] );"
		}
		elsif match Stmt ['printb' F: factor ';' ] {
			Stmt = cast<print_stmt> cons c::print_stmt
				"fwrite( [c_factor(F)], 1, pos, stdout );"
		}
		elsif match Stmt ['prints' F: factor ';' ] {
			Stmt = cast<print_stmt> cons c::print_stmt
				"printf( \"%s\", [c_factor(F)] );"
		}
		elsif match Stmt ['print_token' ';' ] {
			Stmt = cast<print_stmt> cons c::print_stmt
				"fwrite ( ts , 1 , te - ts , stdout );" 
		}
	}
}

end


OutputFile: str = argv.pop()
InputFile: str = argv.pop()
Lang: str = argv.pop()

Input: stream = open( InputFile "r" )
Output: stream = open( OutputFile "w" )

RagelTree: indep::start = parse indep::start[ Input ]

if ( !RagelTree ) {
	print( error '\n' )
	exit(1)
}

# Find the machine name.
MachineName: ragel::machine_name = ragel::machine_name in RagelTree

# Do we need EOF?
NeedsEof: bool = false
for CommVal: indep::comm_val in RagelTree {
	if $CommVal ==
		'@NEEDS_EOF: yes\n'
	{
		NeedsEof = true
	}
}

send Output
	"/*
	" * @LANG: [Lang]
	" * @GENERATED: yes
	" */
	"
	"#include <string.h>
	"#include <stdio.h>
	"

for Section: indep::section in RagelTree {
	if match Section [Decl: var_decl] {
		c_rewrite( Decl )
		send Output [Decl]
	}
}

for Section: indep::section in RagelTree {
	if match Section [section_multiline] {
		for Action: ragel::action_block in Section {
			# Reparse as lang-independent code.
			parse SectionList: indep::section*[$Action]
			if ( !SectionList ) {
				print( error '\n' Action )
				exit(1)
			}

			# Translate to specific language.
			c_rewrite( SectionList )

			# Reparse back to ragel action block.
			Action = parse ragel::action_block[$SectionList]
			if ( !Action ) {
				print( error '\n' )
				exit(1)
			}
		}

		send Output ["\n" Section "\n"]
	}
}

send Output
	"%% write data;
	"int cs;
	"
	"void init()
	"{

for Section: indep::section in RagelTree {
	if match Section [ExprStmt: expr_stmt] {
		c_rewrite( ExprStmt )
		send Output [ExprStmt]
	}
}

send Output
	"	%% write init;
	"}
	"
	"void exec( char *data, int len )
	"{
	"	char *p = data;
	"	char *pe = data + len;

if NeedsEof {
	send Output
		"	char *eof = pe;
}

send Output
	"	%% write exec;
	"}
	"
	"void finish( )
	"{
	"	if ( cs >= [$MachineName.word]_first_final )
	"		printf( \"ACCEPT\\n\" );
	"	else
	"		printf( \"FAIL\\n\" );
	"}
	"

send Output
	"char *inp\[\] = {

NR: int = 0
for InputString: indep::input_string in RagelTree {
	send Output 
		[^InputString ",\n"]
	NR = NR + 1
}

send Output
	"};
	"

send Output
	"int inplen = [NR];
	"

send Output
	"int main( )
	"{
	"	int i;
	"	for ( i = 0; i < inplen; i++ ) {
	"		init();
	"		exec( inp\[i\], strlen(inp\[i\]) );
	"		finish();
	"	}
	"	return 0;
	"}
	"

send Output "/* _____OUTPUT_____
for OutputLine: indep::output_line in RagelTree {
	send Output [OutputLine]
}
send Output "*/


