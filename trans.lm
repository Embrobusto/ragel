include '../src/ragel.lm'

lex 
	literal '%%{', '}%%'
	literal 'int', 'bool', 'char', 'ptr', 'printi', 'prints', 'printf'

	literal ';', '<', '>', '(', ')'

	ignore / ( [ \t] | NL )+ /

	rl INPUT /'_'+ 'INPUT' '_'+/
	rl OUTPUT /'_'+ 'OUTPUT' '_'+/

	token tk_ident /ident/
	token tk_number /digit+/
	token tk_hex_number /'0x' [0-9a-fA-F]+/

	token output
		/ '#ifdef' ' '* OUTPUT any* :>> ( '#endif' ) /

	token input_id
		/ '#ifdef' ' '* INPUT /

	token endif
		/ '#endif' /

	token tk_cpp_comment
		/ cpp_comment /

	token c_comm_open
		/ '/*' /

	token string
		/ s_literal | d_literal /

	token indep_any / any /
end

lex
	token comm_val /'@' [A-Za-z0-9_]+ ': ' [^\n]+ '\n' /
	token comm_term /'*/'/
	token comm_any / any /
end

def comm_item
	[comm_val]
|	[comm_any]

def c_comment
	[c_comm_open comm_item* comm_term]

def input_string
	[string]

def input_list
	[input_list input_string]
| 	[input_string]

def input
	[input_id input_list endif]

def tok
	[tk_ident]
|	[tk_number]
|	[tk_hex_number]
|	[input]
|	[output]
|	[tk_cpp_comment]
|	[c_comment]
|	[string]
|	['<' type '>' '(' tok* ')']
|	['(' tok* ')']
|	['printi' tok ';']
|	['prints' tok ';']
|	['printf' '(' tok* ')' ';']
|	[indep_any]
|	[';']
|	['<']
|	['>']

def section_multiline
	['%%{' ragel::ragel_start '}%%']

def type
	['int'] | ['bool'] | ['char'] | ['ptr']

def var_decl
	[type tk_ident ';']

def stmt
	[var_decl]

def section
	[section_multiline]
|	[stmt]
|	[tok]

OutputFile: str = argv.pop()
InputFile: str = argv.pop()
Lang: str = argv.pop()

def start
	[section*]

Input: stream = open( InputFile "r" )
Output: stream = open( OutputFile "w" )

RagelTree: start = parse start[ Input ]

if ( !RagelTree ) {
	print( error '\n' )
	exit(1)
}

# Find the machine name.
MachineName: ragel::machine_name = ragel::machine_name in RagelTree

# Do we need EOF?
NeedsEof: bool = false
for CommVal: comm_val in RagelTree {
	if $CommVal ==
		'@NEEDS_EOF: yes\n'
	{
		NeedsEof = true
	}
}

prints( Output
	"/*
	" * @LANG: [Lang]
	" * @GENERATED: yes
	" */
	"
	"#include <string.h>
	"#include <stdio.h>
	"
)

int indepToHost( Indep: ref<section*> )
{
	for Decl: var_decl in Indep {
		if match Decl
			"bool [Ident: tk_ident];
		{
			Decl = cons var_decl
				"int [Ident];
		}
	}

	for Tok: tok in Indep {
		if match Tok "true"
			Tok = cons tok "1"
		elsif match Tok "false"
			Tok = cons tok "0"
		elsif match Tok ['<' type '>' '(' TokList: tok* ')']
			Tok = cons tok ['(' TokList ')']
		elsif match Tok ['printi' V: tok ';' ] {
			cons VL: tok*[V]
			Tok = cons tok[ "printf( \"%d\", [V] );"]
		}
		elsif match Tok ['prints' V: tok ';' ] {
			cons VL: tok*[V]
			Tok = cons tok[ "printf( \"%s\", [V] );"]
		}
			
	}
}

for Section: section in RagelTree {
	if match Section [Decl: var_decl] {
		cons SectionList: section* [Decl]
		indepToHost( SectionList )
		prints( Output SectionList )
	}
}

for Section: section in RagelTree {
	if match Section [section_multiline] {
		for Action: ragel::action_block in Section {
			# Reparse as lang-independent code.
			parse SectionList: section*[$Action]

			# Translate to specific language.
			indepToHost( SectionList )

			# Reparse back to ragel action block.
			Action = parse ragel::action_block[$SectionList]
		}

		prints( Output '\n' Section '\n' )
	}
}

prints( Output
	"int cs;
	"
	"%% write data;
	"
	"void init()
	"{
)

prints( Output
	"	%% write init;
	"}
	"
	"void exec( char *data, int len )
	"{
	"	char *p = data;
	"	char *pe = data + len;
)

if NeedsEof
	prints( Output "	char *eof = pe;\n" )

prints( Output
	"	%% write exec;
	"}
	"
	"void finish( )
	"{
	"	if ( cs >= [$MachineName.MachineName]_first_final )
	"		printf( \"ACCEPT\\n\" );
	"	else
	"		printf( \"FAIL\\n\" );
	"}
	"
)

prints( Output
	"char *inp\[\] = {
)

NR: int = 0
for InputString: input_string in RagelTree {
	prints( Output ^InputString ',\n' )
	NR = NR + 1
}

prints( Output
	"};
	"
)

prints( Output "int inplen = " NR ";\n\n" )

prints( Output
	"int main( )
	"{
	"	int i;
	"	for ( i = 0; i < inplen; i++ ) {
	"		init();
	"		exec( inp\[i\], strlen(inp\[i\]) );
	"		finish();
	"	}
	"	return 0;
	"}
	"
)

for Section: section in RagelTree {
	if match Section [OutputBlock: output]
		prints( Output OutputBlock )
}
