include '../src/ragel.lm'

namespace indep
	# Opening the test case header.
	lex
		ignore / ( [ \t] | NL )+ /

		token c_comm_open
			/ '/*' /
	end

	# Contents of test case heaer.
	lex
		token comm_val /'@' [A-Za-z0-9_]+ ': ' [^\n]+ '\n' /
		token comm_term /'*/'/
		token comm_any / any /
	end

	lex 
		literal `%%{ `}%%

		literal `int `bool `const `char `ptr `print_int
				`print_buf `print_str `print_token
				`fwrite `first_token_char `byte `if `else

		literal `fgoto `fcall `fnext `fexec

		literal `; `< `> `( `) `[ `] `=
				`* `! `{ `} `+ `- `== `!=
				`>= `<= `,

		ignore / ( [ \t] | NL )+ /

		token tk_ident /ident/
		token tk_number /digit+/
		token tk_hex_number /'0x' [0-9a-fA-F]+/

		rl INPUT /'_'+ 'INPUT' '_'+/
		rl OUTPUT /'_'+ 'OUTPUT' '_'+/

		token ifdef_INPUT
			/ '#ifdef' ' '* INPUT /

		token ifdef_OUTPUT
			/ '#ifdef' ' '* OUTPUT ' '* '\n' / -ni

		token endif
			/ '#endif' /

		token string
			/ s_literal | d_literal /

		ignore / cpp_comment /
		ignore / c_comment /
	end

	lex
		token output_endif / '#endif\n' /
		token output_line /[^\n]* '\n'/
	end

	def comm_item
		[comm_val]
	|	[comm_any]

	def test_header
		[c_comm_open comm_item* comm_term]

	def input_string
		[string]

	def input_list
		[input_list input_string]
	| 	[input_string]

	def input
		[ifdef_INPUT input_list endif]

	def output
		[ifdef_OUTPUT output_line* output_endif]

	def factor
		[`first_token_char]
	|	[tk_ident]
	|	[tk_ident `[ expr `]]
	|	[tk_ident `( expr `)]
	|	[tk_number]
	|	[`- tk_number]
	|	[tk_hex_number]
	|	[string]
	|	[`< type `> `( expr `)]
	|	[`( expr `)]

	def op
		[`<] | [`>] | [`=] | [`!]
	|	[`+] | [`-] | [`*] | [`!=]
	|	[`==] | [`<=] | [`>=]

	def expr_op
		[op factor]

	def expr
		[factor expr_op*]

	def type
		[`int]
	|	[`bool]
	|	[`char]
	|	[`ptr]
	|	[`byte]

	def opt_arr
		[`[ expr `]]
	|	[]

	def var_decl
		[type tk_ident opt_arr `;]

	def opt_sub
		[ `[ expr `] ]
	|	[]

	def expr_stmt
		[tk_ident opt_sub `= expr `;]
	|	[expr `;]

	def if_stmt
		[`if `( expr `) `{ stmt* `} opt_else]

	def opt_else
		[`else `{ stmt* `}]
	|	[]

	def print_stmt
		[`print_int expr `;]
	|	[`print_buf expr `, expr `;]
	|	[`print_str expr `;]
	|	[`print_token `;]

	def ragel_stmt
		[`fgoto tk_ident `;]
	|	[`fcall tk_ident `;]
	|	[`fnext tk_ident `;]
	|	[`fgoto `* expr `;]
	|	[`fcall `* expr `;]
	|	[`fnext `* expr `;]
	|	[`fexec expr `;]

	def stmt
		[var_decl]
	|	[expr_stmt]
	|	[if_stmt]
	|	[print_stmt]
	|	[ragel_stmt]

	def action_block
		[`{ stmt* `}]
	|	[`{ expr `}]

	def section
		[`%%{ ragel::ragel_start `}%%]

	def start
		[test_header Init: stmt* section input output]
end

int rw_factor( Factor: indep::factor )
{
	print( Factor '\n' )
	return nil
}

int rw_expr( Expr: indep::expr )
{
	print( "expr\n" )
	rw_factor( Expr.factor )
	for ExprOp: indep::expr_op in repeat(Expr._repeat_expr_op)
		rw_factor( ExprOp.factor )
}

int rw_if_stmt( Stmt: indep::if_stmt )
{
	print( "if_stmt\n" )
}

int rw_print_stmt( Stmt: indep::print_stmt )
{
	print( "if_stmt\n" )
}

int rw_stmt( Stmt: indep::stmt )
{
	print( "stmt\n" )
	if match Stmt [expr_stmt]
	{
		print( "expr_stmt\n" )
		rw_expr( Stmt.expr_stmt.expr )
	}
	else if match Stmt [var_decl]
	{
		print( "var_decl\n" )
		rw_expr( Stmt.expr_stmt.expr )
	}
	else if match Stmt [if_stmt]
	{
		rw_if_stmt( Stmt.if_stmt )
	}
	else if match Stmt [print_stmt]
	{
		rw_print_stmt( Stmt.print_stmt )
	}
}

int rw_stmt_list( StmtList: indep::stmt* )
{
	print( "stmt_list\n" )
	for Stmt: indep::stmt in repeat( StmtList )
		rw_stmt( Stmt )
}

int rw_action_block( ActionBlock: indep::action_block )
{
	if match ActionBlock [`{ stmt* `}] {
		rw_stmt_list( ActionBlock._repeat_stmt )
	}
	else if match ActionBlock [`{ expr `}] {
		rw_expr( ActionBlock.expr )
	}
}
