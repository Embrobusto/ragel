include '../src/ragel.lm'

lex 
	literal '%%{', '}%%'
	literal 'int', 'bool', 'const', 'char', 'ptr', 'printi',
			'printb', 'prints', 'print_token', 'printf',
			'fwrite', 'first_token_char'
	literal ';', '<', '>', '(', ')', '[', ']', '=', '*'

	ignore / ( [ \t] | NL )+ /

	token tk_ident /ident/
	token tk_number /digit+/
	token tk_hex_number /'0x' [0-9a-fA-F]+/

	rl INPUT /'_'+ 'INPUT' '_'+/
	rl OUTPUT /'_'+ 'OUTPUT' '_'+/

	token ifdef_INPUT
		/ '#ifdef' ' '* INPUT /

	token ifdef_OUTPUT
		/ '#ifdef' ' '* OUTPUT ' '* '\n' / ni

	token endif
		/ '#endif' /

	token tk_cpp_comment
		/ cpp_comment /

	token c_comm_open
		/ '/*' /

	token string
		/ s_literal | d_literal /

	token indep_any / any /
end

lex
	token output_endif / '#endif\n' /
	token output_line /[^\n]* '\n'/
end


lex
	token comm_val /'@' [A-Za-z0-9_]+ ': ' [^\n]+ '\n' /
	token comm_term /'*/'/
	token comm_any / any /
end

def comm_item
	[comm_val]
|	[comm_any]

def c_comment
	[c_comm_open comm_item* comm_term]

def input_string
	[string]

def input_list
	[input_list input_string]
| 	[input_string]

def input
	[ifdef_INPUT input_list endif]

def output
	[ifdef_OUTPUT output_line* output_endif]

def tok
	[tk_ident]
|	[tk_ident '[' tok ']']
|	[tk_number]
|	[tk_hex_number]
|	[input]
|	[output]
|	[tk_cpp_comment]
|	[c_comment]
|	[string]
|	['<' type '>' '(' tok* ')']
|	['(' tok* ')']
|	['printi' tok ';']
|	['printb' tok ';']
|	['prints' tok ';']
|	['print_token' ';']
|	['first_token_char']
|	['printf' '(' tok* ')' ';']
|	['fwrite' '(' tok* ')' ';']
|	[';']
|	['<']
|	['>']
|	['[']
|	[']']
|	['=']
|	['*']
|	[indep_any]

def section_multiline
	['%%{' ragel::ragel_start '}%%']

def type
	['int']
|	['bool']
|	['char']
|	['char' '*']
|	['const' '(' 'char' ')' '*']
|	['ptr']

def opt_arr
	['[' tok ']']
|	[]

def var_decl
	[type tk_ident opt_arr ';']

def expr_stmt
	[tk_ident '=' tok* ';']

def stmt
	[var_decl]
|	[expr_stmt]

def section
	[section_multiline]
|	[stmt]
|	[tok]

def start
	[section*]

